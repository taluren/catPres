Input=  String


String = str:(content) {return str;}
content = inside:contentUnit* {
    var str="";
    var out=[];
    inside.forEach(function(unit) {
      if (typeof unit=="string") {
        str+=unit;
      } else {
        out.push(str);
        out.push(unit);
        str="";
      }
    });
    out.push(str);
    return out;
  }
contentUnit=
    block
    /mathBlock
    /arrayToken
    /newLineToken
    /commentBlock
    /otherChar

block = open:open str:content close:close {return {param:open.param, id:open.id, inside:str}}

open= (escape b:paramStr id:maybeId openChar {return {param:b, id:id}})/(openChar {return {param:null, id:null}})

maybeId = id/""
id = "#" id:idChar* {return id.join("")}
close=closeChar {return "[[close]]"} 
openChar="{"
closeChar="}" 
paramStr=  _ s:paramSequence _ {return s} / _
paramSequence = severalParamPairs / singleParamPair
severalParamPairs = x:(keyValue/anyKeyword/keyNoValue) _ "," _ y:paramSequence {y[x[0]]=x[1]; return y}
singleParamPair = x:(keyValue/anyKeyword/keyNoValue) {var  y={}; y[x[0]]=x[1]; return y}
keyValue = a:(paramChar+) _ ":" _ b:paramValue {return [a.join(''), b]}
keyNoValue = a:(paramChar+) {return [a.join(''), true]}
paramValue = 
 dquote b:((paramChar/" ")+) dquote {return b.join('')}
 / b:((paramChar)+) {return b.join('')}
paramChar=[a-zA-Z0-9_\[\]\-]
idChar=[a-zA-Z0-9_]
dquote='"'

anyKeyword = 
//weightKeyword = 
  ("bold"/"textbf") {return ["weight","bold"]}
//fontKeyword = 
  / c:("sans"/"serif") {return ["font",c]}
  
//colorKeyword = 
//  / c:([a-z]+) {return ["color", c.join('')]}
//sizeKeyword = 
//  / c:([0-9]+) {return ["size", c.join('')]}




mathBlock = mathChar content:mathContent mathChar {return {math:content}}
mathContent = math:((!mathChar) c:. {return c})* {return math.join('')} 
mathChar= "$"

arrayToken = c:arrayChar indenter:indenter {return {array:c, indenter:indenter}}
arrayChar = ("&"/"//")

newLineToken = "\n" indenter:indenter {return {newline:true, indenter:indenter}}

commentBlock = commentChar ((!'\n') .)* '\n'? {return ""}
commentChar= "%"
  
indenter = fullIndenter/spaceIndenter

fullIndenter = indent:(indentChar*) id:maybeId endOfIndent
              {return {indent:indent.join(''), id:id, spaceAfter:null}}
              
              
spaceIndenter = sp:_ {return {indent:null, id:"", spaceAfter:sp}}
           
indentChar=(! (endOfIndent/simpleNewLine/escape)) c:otherChar {return c}
endOfIndent = "|"
simpleNewLine = "\n" sp:_ {return {newline:{indent:null}, spaceAfter:sp}}

otherChar = !open!close c:(escapedChar / simpleChar) {return c}
simpleChar = !special c:. {return c} 
escapedChar = escape  c:special {return c}
escape = "\\"
special = [#&]/openChar/closeChar/mathChar/arrayChar/commentChar

_ "space"= s:space* {return s.join('')}
space =[ \t]